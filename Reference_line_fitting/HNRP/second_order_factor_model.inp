TITLE: Second Order Factor Analysis;
DATA: FILE IS data.dat;
VARIABLE: 
NAMES ARE 
GPEGDOM 
GPEGNDOM 
STRP1GV 
STRP2GV 
STRP3GV 
TRLA 
TRLB 
PASAT 
HVLTTOT 
HVLTDELR 
VFLU 
SEMFLU 
AFLU;
MODEL:
MEM BY HVLTTOT* HVLTDELR;
MEM(var_MEM);
VerPS BY STRP1GV* STRP2GV STRP3GV;
VerPS(var_VerPS);
ATT_PS BY TRLA* TRLB PASAT;
ATT_PS(var_ATT_PS);
MOT BY GPEGDOM* GPEGNDOM;
MOT(var_MOT);
VERB BY SEMFLU* VFLU AFLU;
VERB(var_VERB);
G BY 
MEM*(G_MEM)
VerPS(G_VerPS)
ATT_PS(G_ATT_PS)
MOT(G_MOT)
VERB(G_VERB);
G@1;
[G@0];
MODEL CONSTRAINT:
var_MEM = 1 - G_MEM^2;
var_VerPS = 1 - G_VerPS^2;
var_ATT_PS = 1 - G_ATT_PS^2;
var_MOT = 1 - G_MOT^2;
var_VERB = 1 - G_VERB^2;
ANALYSIS:
ESTIMATOR = MLR;
OUTPUT: STDYX RESIDUAL;
SAVEDATA:
RESULTS ARE second_order_factor_model_results.txt;
save=fscores;
file=second_order_factor_model_fscore.dat;
